C     PROGRAM NO.: F-32
C
C     STANDARD 1984 VERSION OF CANADIAN FOREST FIRE WEATHER INDEX SYSTEM
C     AS OF 28-JANUARY-1986.
C
C     WRITTEN IN FORTRAN 77.
C
C     READS WEATHER DATA IN EITHER ENGLISH OR METRIC UNITS AND PRINTS
C     OUT IN METRIC ONLY.
C
C     CODES AND INDEXES OUTPUT ALL TO ONE DECIMAL PLACE.
C     DAILY SEVERITY RATING OUTPUT TO TWO DECIMAL PLACES.
C
C     LMON = LENGTH OF MONTHS
C     EL = DMC DAY-LENGTH FACTORS
C     FL = DC DAY-LENGTH FACTORS
C
      DIMENSION LMON(12), EL(12), FL(12),AST(2),TITLE(20)
      LOGICAL*1 DAT(9),YES,YES1,ANS,ANS1
      LOGICAL *1 INFMT(40)
      ! Hardcode input filename
      OPEN(UNIT=1,FILE='f32in.dat',status='old',action='read')
      OPEN(UNIT=2,FILE='f32out_w85.dat',status='replace',action='write')
      DATA LMON /31,28,31,30,31,30,31,31,30,31,30,31/
      DATA EL /6.5,7.5,9.0,12.8,13.9,13.9,12.4,10.9,9.4,8.0,7.0,
     *6.0/
      DATA FL /-1.6,-1.6,-1.6,.9,3.8,5.8,6.4,5.0,2.4,.4,-1.6,-1.6/

      IUNIT=0    ! Hardcode to metric units
C
C     20 - METRIC FORMAT, 25 - ENGLISH FORMAT
C
   20 FORMAT(F4.1,2I4,F5.1)
   25 FORMAT(F4.0,2I4,F5.2)
C
C     READS IN STATION & YEAR.
C
      READ(1,30) TITLE
   30 FORMAT(20A4)
C
C     THIS SECTION ALLOWS FOR INITIAL VALUES OF FFMC, DMC, DC TO BE OPTIONAL 
C
      ! Hardcode initial values
      FO=85.0
      PO=6.0
      DOT=15.0

C
C     READS STARTING MONTH OF THE YEAR AND NUMBER OF DAYS IN STARTING
C     MONTH.
C
   55 READ(1,60) M, NDAYS 
   60 FORMAT(I1,I2)
      WRITE(2,65)
   65 FORMAT(1H1'PROGRAM NO.: F-32')
C      CALL DATE(DAT)
C      WRITE(2,70) DAT,TITLE
   70 FORMAT(1X,9A1///1X,20A4//)
      WRITE(2,75) FO,PO,DOT
   75 FORMAT(' INITIAL VALUES FOR FFMC:',F5.1,', DMC:',F5.1,', DC:',F5.1,
     *//)
      DO 290 J=M,12
      NN=LMON(J)
      IF(J.EQ.M) GO TO 80 
      IDAYS=1
      GO TO 85
   80 IDAYS=LMON(J)-NDAYS+1
C
C     READS DAILY WEATHER DATA
C
   85 IAST=1
      DO 290 I=IDAYS,NN
      IF(IUNIT.EQ.0) GO TO 90 
      READ(1,25,END=295) T,IH,IW,R
      W=IW
      GO TO 95
   90 READ(1,20,END=295) T,IH,IW,R
!      print *,T,IH,IW,R
      W=IW
   95 IF(IUNIT.EQ.0) GO TO 100 
      T=(T-32.)*5./9.
      W=W*1.609
      R=R*25.4
  100 TX=T
      H=IH
      RAIN=R
      IF(IAST.GT.1) GO TO 110 
      WRITE(2,105)
  105 FORMAT(///,1X1X,' DATE  TEMP  RH  WIND   RAIN   FFMC   DMC',
     *   '     DC   ISI   BUI   FWI     DSR'/)
C
C FINE FUEL MOISTURE CODE
C
  110 WMO=(147.2*(101-FO))/(59.5+FO)
      IF(R.GT.0.5) GO TO 115
      GO TO 125 
  115 RA=R-.5
      IF(WMO.GT.150.) GO TO 120
      WMO=WMO+42.5*RA*ExP(-100./(251.-WMO))*(1.-EXP(-6.93/RA))
      GO TO 125
  120 WMO=(WMO+42.5*RA*ExP(-100./(251.-WMO))*(1.-EXP(-6.93/RA)))+ 
     *(0.0015*(WMO-150.)**2)*RA**.5
  125 IF(WMO.GT.250.) WMO=250.
      ED=0.942*(H**0.679)+(11.*EXP((H-100.)/10.))+0.18*(21.1-T) 
     **(1.-1./ExP(0.115*H))
      IF(WMO-ED) 130,135,140
  130 EW=.618*(H**.753)+(10.*ExP((H-100.)/10.))+.18*(21.1-T) 
     **(1.-1./EXP(0.115*H))
      IF(WMO.LT.EW) GO TO 145
  135 WM=WMO
      GO TO 150
  140 Z=0.424*(1.-(H/100.)**1.7)+(0.0694*(W**0.5))*(1.-(H/100.)**8)  
      X=Z*(0.581*(EXP(0.0365*T)))
      WM=ED+(WMO-ED)/10.**X
      GO TO 150
  145 z=.424*(1.-((100.-H)/100.)**1.7)+(.0694*(W**.5))*(1.-((100.-H) 
     */100.)**8)
      X=Z*(.581*(EXP(.0365*T))) 
      WM=EW-(EW-WMO)/10.**x
  150 FFM=(59.5*(250.-WM))/(147.2+WM)
      IF(FFM.GT.101.) GO TO 155 
      IF(FFM) 160,165,165
  155 FFM=101.
      GO TO 165
  160 FFM=0.0
C
C DUFF MOISTURE CODE
C
  165 IF(T+1.1.GE.0.) GO TO 170 
      T=-1.1
  170 RK=1.894*(T+1.1)*(100.-H)*(EL(J)*0.0001)
  175 IF(R.GT.1.5) GO TO 180
      PR=PO
      GO TO 205 
  180 RA=R
      RW=0.92*RA-1.27
      WMI=20.0+280./EXP(0.023*PO)
      IF(PO.LE.33.) GO TO 185
      IF(PO-65.) 190,190,195
  185 B=100./(0.5+0.3*PO)
      GO TO 200
  190 B=14.-1.3*ALOG(PO)
      GO TO 200
  195 B=6.2*ALOG(PO)-17.2
  200 WMR=WMI+(1000.*RW)/(48.77+B*RW) 
      PR=43.43*(5.6348-ALOG(WMR-20.))
  205 IF(PR.GE.0.) GO TO 210
      PR=0.0
  210 DMC=PR+RK
C 
C     DROUGHT CODE
C
      IF(T+2.8.GE.0.) GO TO 215
      T=-2.8
  215 PE=(.36*(T+2.8)+FL(J))/2.
      IF(R.LE.2.8) GO TO 225
      RA=R
      RW=0.83*RA-1.27
      SMI=800.*EXP(-DOT/400.)
      DR=DOT-400.*ALOG(1.+(3.937*RW)/SMI)   ! Extra r-paren added here
      IF(DR.GT.0.) GO TO 220
      DR=0.0
  220 DC=DR+PE
      GO TO 230
  225 DR=DOT
      GO TO 220
  230 IF(DC.GE.0.) GO TO 235
      DC=0.0

C
C      INITIAL SPREAD INDEX, BUILDUP INDEX, FIRE WEATHER             INDEX
C

  235 FM=(147.2*(101.-FFM))/(59.5+FFM)
      SF=19.115*EXP(FM*(-.1386))*(1.+(FM**5.31)/4.93E07)
      SI=SF*EXP(0.05039*W)
  240 IF(DMC.EQ.0.0.AND.DC.EQ.0.0) GO TO 245
      BUI=(0.8*DC*DMC)/(DMC+0.4*DC)
      GO TO 250
  245 BUI=0.
  250 IF(BUI.GE.DMC) GO TO 255
      P=(DMC-BUI)/DMC
      CC=0.92+(0.0114*DMC)**1.7 
      BUI=DMC-(CC*P)
      IF(BUI.LT.0.) BUI=0.
  255 IF(BUI.GT.80.)     GO TO 260
      BB=0.1*SI*(0.626*BUI**0.809+2.)
      GO TO 265
  260 BB=0.1*SI*(1000./(25.+108.64/EXP(0.023*BUI)))
  265 IF(BB-1.0.LE.0.) GO TO 270 
       SL=2.72*(0.434*ALOG(BB))**0.647
       FWI=EXP(SL)

       GO TO 275

  270 FWI=BB
  275 DSR = 0.0272*FWI**1.77
  280 IW=W+0.5

      WRITE(2,285) J,I,TX,IH,IW,RAIN,FFM,DMC,DC,SI,BUI,FWI,DSR
  285 FORMAT(1X,2I3,F6.1,I4,I6,F7.1,F7.1,F6.1,F7.1,3F6.1,F8.2) 
      FO=FFM
      PO=DMC 
      DOT=DC 
      IAST=IAST+1
  290 CONTINUE 
  295 PRINT *,'DONE'
  305 STOP
      END
